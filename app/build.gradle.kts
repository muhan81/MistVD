import java.util.Properties

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    kotlin("kapt")
}

// Assinatura de release: lida de NekoVideo/release-signing.properties (gitignored).
// Sem o arquivo (ex.: outro clone), cai no signingConfig de debug para que
// assembleRelease sempre produza um APK instalavel em vez de um -unsigned.
val keystorePropsFile = rootProject.file("release-signing.properties")
val keystoreProps = Properties().apply {
    if (keystorePropsFile.exists()) {
        keystorePropsFile.inputStream().use { load(it) }
    }
}

android {
    namespace = "com.nkls.nekovideo"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.nkls.nekovideo"
        minSdk = 30
        targetSdk = 36
        versionCode = 49
        versionName = "1.21.2"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (keystorePropsFile.exists()) {
            create("releaseLocal") {
                storeFile = rootProject.file(keystoreProps.getProperty("storeFile"))
                storePassword = keystoreProps.getProperty("storePassword")
                keyAlias = keystoreProps.getProperty("keyAlias")
                keyPassword = keystoreProps.getProperty("keyPassword")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
            resValue("string", "app_name", "MistVD Debug")
        }
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            isDebuggable = false

            signingConfig = if (keystorePropsFile.exists()) {
                signingConfigs.getByName("releaseLocal")
            } else {
                signingConfigs.getByName("debug")
            }

            ndk {
                debugSymbolLevel = "full"
                abiFilters += "arm64-v8a"
            }
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += listOf(
            "-opt-in=androidx.compose.foundation.ExperimentalFoundationApi",
            "-opt-in=androidx.compose.material3.ExperimentalMaterial3Api"
        )
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    // Performance otimizations
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.ui)
    implementation(libs.androidx.ui.graphics)
    implementation(libs.androidx.ui.tooling.preview)
    implementation(libs.androidx.material3)
    implementation(libs.androidx.navigation.compose.android)
    implementation(libs.androidx.foundation.android)
    implementation(libs.gson)
    implementation(libs.androidx.biometric)
    implementation(libs.androidx.room.runtime)
    implementation(libs.androidx.room.ktx)

    implementation(libs.androidx.material.icons.extended)
    implementation(libs.coil.video)
    implementation(libs.coil.compose)
    implementation(libs.androidx.foundation.android)
    implementation(libs.androidx.media3.common.ktx)
    implementation(libs.androidx.media3.exoplayer)
    implementation(libs.androidx.media3.ui)
    implementation(libs.androidx.media3.session)
    implementation(libs.androidx.media3.transformer)
    // 第 6 轮美颜：提供 GlEffect / setVideoEffects 所需的特效体系。
    // media3-exoplayer 不会自动带上它，必须显式声明，且版本与其余 media3 模块一致。
    implementation(libs.androidx.media3.effect)
    implementation(libs.material.icons.extended.v168)

    implementation(libs.glide)

    implementation(libs.androidx.navigation.compose)
    implementation(libs.androidx.animation)

    implementation(libs.androidx.mediarouter)
    implementation(libs.nanohttpd)
    implementation(libs.material)

    kapt(libs.androidx.room.compiler)

    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.ui.test.junit4)
    debugImplementation(libs.androidx.ui.tooling)
    debugImplementation(libs.androidx.ui.test.manifest)
}
